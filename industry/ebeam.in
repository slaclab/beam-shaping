OPTION, ECHO=FALSE; 
OPTION, INFO=FALSE;		//psdump and statdump are in time steps
OPTION, STATDUMPFREQ = 10;	//How often beam stats dumped to .stat.  
OPTION, PSDUMPFREQ = 500;
OPTION, AUTOPHASE=4;		//Always leave this on, unless doing a phase scan
Option, VERSION=20200;
Option, ENABLEHDF5=TRUE; 

Title, string="LCLS2";

//------------------------------------------------------------------------
//					Global Parameters

REAL rf_freq         	     = 187.0e6;        //RF frequency. (Hz)
REAL n_particles             = 10.0e3;            //Number of particles in simulation.
REAL beam_bunch_charge       = 1.0*1e-9;    //Charge of bunch. (C)
REAL beam_current            = rf_freq*beam_bunch_charge*1e6;

//Initial energy Calc
REAL Edes    = 1.4e-9;
REAL gamma   = (Edes+EMASS)/EMASS;
REAL beta    = sqrt(1-(1/gamma^2));
REAL P0      = gamma*beta*EMASS;

value , {Edes, P0}; 					

//Input components for LCLS2 - EIC area
//-------------------------------------------------------------------------------------
// Gun
//
// Cavity/RF field.
//
// L:           physical element length (real in m). Length (of field map) (m).
// VOLT:        field scaling factor (real). RF field magnitude (MV/m).
// FMAPFN:      field file name (string)
// ELEMEDGE:        physical start of the element on the floor (real in m)
// TYPE:        specifies "STANDING" (default), "TRAVELLING" or "SINGLE GAP" structure
// FREQ:        RF frequency of cavity (real in MHz). Resonance frequency.
// LAG:         cavity phase (radians)
//

REAL deg=PI/180.0;

GUN:    RFCavity, L = 0.2, VOLT = -1.0, ELEMEDGE = 0.0, TYPE = "STANDING",
        FMAPFN = "DCGUN.DAT",
        FREQ = 1.0e-6;

//-------------------------------------------------------------------------------------
// Solenoids
//
// L:           Physcial element length (m)
// ELEMEDGE:    Physcial start of element (m)
// KS:          Solenoid strength (T/m)
// FMAPFM:      Field file (string)
// Element edge should be center of device

SOL1:   Solenoid, L = 0.48, ELEMEDGE= 0.6,  KS = 0.0045,
        FMAPFN = "SOL.DAT";

DR1: DRIFT, L = 4.0, ELEMEDGE = 1.0; 

//--------------------------------------------------------------------------------
// Accelerator lines
Col:ECOLLIMATOR, L=5.0, ELEMEDGE=0.0, XSIZE=30E-3,
    YSIZE=30E-3, OUTFN="col.h5";

EBEAM:  Line = (Col, GUN, SOL1, DR1);
//-------------------------------------------------------------------------------------

// Distribution definition
Dist:DISTRIBUTION, TYPE = FROMFILE,
                   FNAME = "opal_target_dist_10ns.txt",
                   EMITTED = TRUE,
                   EMISSIONMODEL = NONE,
                   EMISSIONSTEPS = 100,
                   EKIN=0;

//-------------------------------------------------------------------------------------
// Define Field solvers
// The mesh sizes should be a factor of 2 for most efficient space charge (SC) calculation.

FS_SC: Fieldsolver, FSTYPE = FFT, 
                    MX = 16, MY = 16, MT = 32,
		            PARFFTX = false, 
		            PARFFTY = false, 
		            PARFFTT = true,
		            BCFFTX = open, 
		            BCFFTY = open, 
		            BCFFTT = open,
		            BBOXINCR = 1, 
		            GREENSF = INTEGRATED;

//-------------------------------------------------------------------------------------
// Beam Definition

BEAM1: BEAM, PARTICLE = ELECTRON,
       pc = P0, NPART = n_particles, BFREQ = rf_freq,
       BCURRENT = beam_current, CHARGE = -1;

//-------------------------------------------------------------------------------------
// Run beamline

TRACK, LINE = EBEAM, BEAM = BEAM1, MAXSTEPS = 1900000, DT = {5.0e-13, 5.0e-12}, ZSTOP={0.2, 5.0};
RUN, METHOD = "PARALLEL-T", BEAM = BEAM1, FIELDSOLVER = FS_SC, DISTRIBUTION = Dist;
ENDTRACK;

Stop; 
Quit;
